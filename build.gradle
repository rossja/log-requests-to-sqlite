buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:4.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'

group 'eu.righettod'
version '1.0.5'
def burpExtensionHomepage = 'https://github.com/righettod/log-requests-to-sqlite'
def burpExtensionJarName = 'LogRequestsToSQLite.jar'

repositories {
    mavenCentral()
}

dependencies {
    compile 'net.portswigger.burp.extender:burp-extender-api:1.7.13'
    compile 'org.xerial:sqlite-jdbc:3.23.1'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

compileJava {
    targetCompatibility '1.8'
    sourceCompatibility '1.8'
}

dependencyCheck {
    autoUpdate = true
    failOnError = true
    failBuildOnCVSS = 1
    cveValidForHours = 12
    format = 'JSON'
}

task fatJar(type: Jar) {
    manifest {
        attributes("Implementation-Version": project.version, "Implementation-Title": project.name, "Implementation-URL": burpExtensionHomepage)
    }
    archiveName = burpExtensionJarName
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
